graph Main [[ main ]]
{
    input event std::midi::Message midiIn;
    output stream float out;

    input value float feedback [[ name: "feedback", min: 0.80, max: .99, init: 0.99]];

    node gate = MidiGate;
    node noiseSource = std::noise::White;
    node delay = Delay;

    connection
    {
        midiIn -> std::midi::MPEConverter -> NoteSelector -> delay.delayLength, gate.gateLength;
        midiIn -> std::midi::MPEConverter -> gate.eventIn;

        noiseSource.out * gate.out -> delay.in;
        delay.out * feedback -> [1] -> delay.in;
        delay.out -> out;
    }
}

processor MidiGate
{
    input event (std::notes::NoteOn, std::notes::NoteOff) eventIn;
    input event float gateLength;
    input event float gateMultiplier;
    output stream float out;

    event eventIn (std::notes::NoteOn e)        { openGate = true; }
    event gateLength (float gateLength)         { gateDurationInSamples = gateLength * gateScale; }
    event gateMultiplier (float gateMultiplier) { gateScale = gateMultiplier; }

    bool openGate = false;
    float gateDurationInSamples;
    float gateScale;

    void main()
    {
        loop
        {
            if (openGate)
            {
                out <- 1.0f;
                gateDurationInSamples--;
                if (gateDurationInSamples <= 0) { openGate = false; }
            }
            advance();
        }
    }
}

processor NoteSelector ()
{
    input event (std::notes::NoteOn, std::notes::PitchBend) eventIn;
    output event float noteFrequency;

    float notePitch, pitchBend;

    event eventIn (std::notes::NoteOn noteOn)
    {
        notePitch = noteOn.pitch;
        noteFrequency <- std::notes::noteToFrequency (notePitch + pitchBend);
    }

    event eventIn (std::notes::PitchBend e)
    {
        pitchBend = e.bendSemitones;
        noteFrequency <- std::notes::noteToFrequency (notePitch + pitchBend);
    }
}

processor Delay
{
    input  stream float in;
    output stream float out;

    input event float delayLength;

    event delayLength (float delaySamples)  { readPos = wrap<maxDelayLength> (writePos - int(processor.frequency / delaySamples)); }

    let maxDelayLength = 10000;

    float[maxDelayLength] buffer;
    wrap<maxDelayLength> readPos, writePos;

    void main()
    {
        loop
        {
            out <- buffer[readPos];
            buffer[writePos] = in;

            ++readPos;
            ++writePos;

            advance();
        }
    }
}
